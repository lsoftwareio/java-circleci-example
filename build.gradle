buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.0.RELEASE")
	}
}

plugins {
    id 'com.palantir.docker' version '0.19.2'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

def artifact = "hello-webapp"
group = 'io.lsoftware.examples'
version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
    baseName = artifact
    version = version
}

repositories {
	mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-thymeleaf')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    integrationTestCompile 'org.seleniumhq.selenium:selenium-java:3.10.0'
    integrationTestCompile 'org.seleniumhq.selenium:htmlunit-driver:2.29.0'
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    // used to make sure that we always run our integration tests
    outputs.upToDateWhen { false }
}
check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

docker {
    name "lsoftware/${artifact}:${version}"
    files bootJar.archivePath
    buildArgs(['JAR_FILE': "${artifact}-${version}.jar"])
}